# shortcut to this dotfiles path is $DOTFILES_ROOT
export DOTFILES_ROOT=$HOME/.dotfiles

# stash local environment variables in ~/.localrc
if [[ -a ~/.localrc ]]; then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES_ROOT/**/*.zsh)
autoload -Uz $DOTFILES_ROOT/functions/*(:t)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete
autoload -Uz compinit
compinit -C

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

# load plugins
source ~/.zsh/.zsh_plugins
source $DOTFILES_ROOT/zsh/plugins_config

# refresh cache for autocomplete
for dump in $HOME/.zcompdump(#qN.m1); do
  rm $HOME/.zcompdump
  compinit
  touch .zcompdump

  if [[ -s "$dump" && (! -s "$dump.zwc" || "$dump" -nt "$dump.zwc") ]]; then
      zcompile "$dump"
  fi
done

unset config_files

test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"

