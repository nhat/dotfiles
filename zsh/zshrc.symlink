# shortcut to this dotfiles path is $DOTFILES_ROOT
export DOTFILES_ROOT=$HOME/.dotfiles

# stash local environment variables in ~/.localrc
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# oh-my-zsh
ZSH=$HOME/.oh-my-zsh
DISABLE_AUTO_UPDATE=true
DISABLE_AUTO_TITLE=true
plugins=(git history-search-multi-word bd zsh-completions zsh-autosuggestions)
source $ZSH/oh-my-zsh.sh

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES_ROOT/**/*.zsh)
autoload -U $DOTFILES_ROOT/functions/*(:t)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -Uz compinit
compinit -u

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
